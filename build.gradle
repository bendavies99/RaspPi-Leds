plugins {
    id 'application'
    id 'de.gliderpilot.semantic-release' version '1.0.0'
    id 'signing'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
}

group 'net.bdavies'
application.mainClassName = "net.bdavies.Application"
application.applicationName = "RLed"
archivesBaseName = "RaspPi-Leds"

repositories {
    mavenLocal()
    mavenCentral()
}

project.afterEvaluate {
    task javadocJar(type: Jar) {
        dependsOn project.tasks.javadoc
        from project.tasks.javadoc.destinationDir
        archiveClassifier = 'javadoc'
        group 'build'
    }

    task sourceJar(type: Jar) {
        dependsOn project.tasks.classes
        from sourceSets.main.allSource
        archiveClassifier = 'sources'
        group 'build'
    }
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourceJar
                artifact javadocJar
                artifactId archivesBaseName
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                pom {
                    name = 'Babblebot-Server'
                    description = 'This is the core server for Babblebot'
                    url = 'https://github.com/bendavies99/BabbleBot-Server'
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/bendavies99/BabbleBot-Server/issues'
                    }
                    licenses {
                        license {
                            name = 'GPL-3.0'
                            url = 'https://github.com/bendavies99/BabbleBot-Server/LICENSE.md'
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = 'https://github.com/bendavies99/BabbleBot-Server'
                        connection = 'scm:git:git://github.com/bendavies99/BabbleBot-Server.git'
                        developerConnection = 'scm:git:ssh://git@github.com:bendavies99/BabbleBot-Server.git'
                    }
                    developers {
                        developer {
                            name = 'Ben Davies'
                        }
                    }
                }
            }
        }
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        publications = ['maven']
        publish = true
        pkg {
            repo = 'Babblebot-Server'
            name = archivesBaseName
            userOrg = user
            licenses = ['GPL-3.0']
            vcsUrl = 'https://github.com/bendavies99/RaspPi-Leds.git'
            version {
                name = version
                vcsTag = version
            }
        }
    }

    publish.dependsOn(bintrayUpload)
    project.tasks.bintrayUpload.doLast {
        bintrayPublish
    }
}

task createProperties {
    doLast {
        def version = project.version.toString()
        def file = new File("$project.buildDir/resources/main/version.txt")
        if (!file.exists()) {
            println("Ran Create Properties")
            new File("$project.buildDir/resources/main/").mkdirs()
            file.createNewFile()
        }
        file.write(version)
    }
}

run {
    dependsOn createProperties
    standardInput = System.in
}

jar {
    manifest {
        attributes 'Main-Class': 'net.bdavies.Application'
    }


    dependsOn createProperties
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation "com.github.mbelling:rpi-ws281x-java:2.0.0"
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation 'org.apache.logging.log4j:log4j-slf4j18-impl:2.13.0'
    implementation 'org.projectlombok:lombok:1.18.12'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.0'
    implementation 'org.apache.commons:commons-lang3:3.10'
    implementation platform("io.projectreactor:reactor-bom:Dysprosium-SR6")
    implementation 'io.projectreactor.netty:reactor-netty'
    implementation 'com.github.zafarkhaja:java-semver:0.9.0'
    implementation 'commons-io:commons-io:2.6'

    annotationProcessor 'org.projectlombok:lombok:1.18.12'
}
